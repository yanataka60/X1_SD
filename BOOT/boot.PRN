			  Z80 ASSEMBLER - ZASM VER 1.6
  0003                	INPUTF		EQU		0003H	;１行入力
  000B                	PRINT		EQU		000BH	;文字列のプリント
  0013                	ACCPRT		EQU		0013H	;１文字出力
  001B                	INKEY$		EQU		001BH	;１文字入力
  0023                	TAK49S		EQU		0023H	;サブCPU80C49との通信
  002B                	PCG			EQU		002BH	;PCGのセット
  0033                	CGREAD		EQU		0033H	;PCGの内容を読む
  003B                	SAVE1		EQU		003BH	;FCBをCMTにセーブ
  003E                	SAVE2		EQU		003EH	;データをCMTにセーブ
  0041                	LOAD1		EQU		0041H	;FCBをCMTからロード
  0044                	LOAD2		EQU		0044H	;CMTからデータをロード
  0047                	VERFY2		EQU		0047H	;CMTのデータとメイン・メモリのデータを比較
  004A                	BRKCHK		EQU		004AH	;SHIFT+BREAK CHECK
  004D                	WIDTH48		EQU		004DH	;Aレジスタの値により40文字、80文字モードを指定
  012D                	KVECIN		EQU		012DH	;キーボード通信?
  0133                	KVEC00		EQU		0133H	;80C49にキー入力割込みベクトルの下位を伝達
  013C                	PSGINT		EQU		013CH	;PSGの出力を止める
  015A                	BINPUT		EQU		015AH	;１行入力 BASIC用
  0346                	INTKEY		EQU		0346H	;割込みキー入力の実行
  04A3                	CR2			EQU		04A3H	;行の先頭でないなら改行
  04A7                	CR1			EQU		04A7H	;改行
  04AB                	TABPRT		EQU		04ABH	;X座標が10の倍数までスペース出力
  04BA                	SPPRT		EQU		04BAH	;スペース出力
  04C8                	ACCDIS		EQU		04C8H	;１文字出力 コントロール・コード表示版
  054A                	ADRCAL		EQU		054AH	;現在カーソル位置のテキストVRAMのアドレス計算
  054D                	ADRCA2		EQU		054DH	;HLレジスタで示したテキスト座病のテキストVRAMのアドレス計算
  0577                	CTRLJB		EQU		0577H	;コントロール・コード処理
  07F7                	BEEP		EQU		07F7H	;ベル音を鳴らす
  098C                	WIDTH80		EQU		098CH	;WIDTH80
  0998                	WIDTH40		EQU		0998H	;WIDTH40
  09C0                	SCRNOT		EQU		09C0H	;表示画面のページ指定
  09F5                	SCRNIN		EQU		09F5H	;書き込み画面のページ指定
  0A3F                	CTRLDQ		EQU		0A3FH	;コンソールと入出力ページの初期化
  0A5A                	STPRIO		EQU		0A5AH	;パレット、プライオリティ・セット
  0A6B                	CLST		EQU		0A6BH	;テキスト・クリア
  0A8A                	STCLSG		EQU		0A8AH	;グラフィック・クリア(SCRMODの値による)
  0A8F                	CLSG		EQU		0A8FH	;グラフィック・クリア(SCRMODの値によらない)
  0B49                	RECV49		EQU		0B49H	;サブCPU8049からデータを受信
  0B54                	TRANS49		EQU		0B54H	;サブCPU8049に送信要求コードを送る
  0B61                	OBFCK		EQU		0B61H	;80C49がデータを受け取れる状態になるまでウェイト
  0B6B                	IBFCK		EQU		0B6BH	;80C49からデータが送られてくるまでウェイト
  0C8A                	WBYTE		EQU		0C8AH	;CMT 1Byte Write
  0CAA                	RBYTE		EQU		0CAAH	;CMT 1byte Read
  0CD6                	CLRSUM		EQU		0CD6H	;チェックサムバッファのクリア
  0CDF                	GAP			EQU		0CDFH	;CMTへリーダー音の書込み
  0D20                	TMARK		EQU		0D20H	;CMTのリーダー音待ち
  0D8A                	SHORT		EQU		0D8AH	;CMTへ1ビット(0)書き込み
  0DA4                	LONG		EQU		0DA4H	;CMTへ1ビット(1)書き込み
  0DC7                	MOTOR		EQU		0DC7H	;CMT読み書き開始
  0DEC                	CMTCOM		EQU		0DECH	;CMTに対しコマンドを実行
  0DF6                	CMTSNS		EQU		0DF6H	;CMTの状態を示す
  0DFE                	COMOUT		EQU		0DFEH	;サブCPU80C49へコマンド送信
  0E77                	PSGSET		EQU		0E77H	;HLで始まる8バイトのデータをPSGにセット
  0FE2                	MONOP		EQU		0FE2H	;モニタコールドスタート
  1003                	HOTSTA		EQU		1003H	;モニタホットスタート
  111F                	HLSET		EQU		111FH	;バッファ上の文字を16進数に変換してHLレジスタにセット
  1143                	HEXCHL		EQU		1143H	;DEで示すアドレスの文字を16進数に変換してAレジスタにセット
  115E                	ACSET		EQU		115EH	;バッファ上の文字を16進数に変換してAレジスタにセット
  11AF                	DUMP		EQU		11AFH	;HLで示すアドレスからCバイトメモリダンプ
  1202                	HLHXPR		EQU		1202H	;HLレジスタの値を16進数で出力
  1207                	ACHXPR		EQU		1207H	;Aレジスタの値を16進数で出力
  12D5                	CR1LPL		EQU		12D5H	;LFのプリンタ出力
  12DC                	ACCLPL		EQU		12DCH	;プリンタへの1文字出力
  1315                	TABLPL		EQU		1315H	;HTABのプリンタ出力
  1321                	FNPRHL		EQU		1321H	;FCBのファイル・ネームをプリント
  134E                	FNMTCH		EQU		134EH	;ファイル・ネームとパスワードを比較
  1394                	SETDIQ		EQU		1394H	;FCBにファイルネームをセット
  1420                	ACCPRP		EQU		1420H	;画面またはプリンタへの1文字出力
  142F                	PRINTP		EQU		142FH	;文字列を画面またはプリンタに出力
  143C                	TABPRP		EQU		143CH	;HTABを画面またはプリンタに出力
  1446                	CR1PRP		EQU		1446H	;改行コードを画面またはプリンタに出力
  1451                	TUPPER		EQU		1451H	;大文字に変換
                      	
  0006                	LINLIM		EQU		0006H	;1行入力のバッファの長さのリミット R/W
  0007                	WIDTHO		EQU		0007H	;WIDTHの値 R
  000E                	CURX		EQU		000EH	;カーソルのX座標 R/W
  000F                	CURY		EQU		000FH	;カーソルのY座標 R/W
  0016                	CURYST		EQU		0016H	;テキスト表示エリアのY座標の先頭 R/W
  0017                	CURYED		EQU		0017H	;テキスト表示エリアのY座標の終わり R/W
  001E                	CURXST		EQU		001EH	;テキスト表示エリアのX座標の先頭 R/W
  001F                	CURXED		EQU		001FH	;テキスト表示エリアのX座標の終わり R/W
  0026                	COLORF		EQU		0026H	;カラーアトリビュートの値 R/W
  0027                	CLSCHR		EQU		0027H	;NULキャラクタ・コード R/W
  002E                	KEYDAT		EQU		002EH	;割込みキー入力で入ってきたキーコード(ASCII) R
                      				;EQU	002FH	;割込みキー入力で入ってきたキーコード(ファンクション・コード) R
  0036                	BRKBUF		EQU		0036H	;割込みキー入力によって入力されるBREAKおよびCTRL+Sを知らせるバッファ R
  0037                	KEYFLG		EQU		0037H	;有効なキーが割り込んだ時は0以外、無効なキーが割り込んだ時は0 R
  00E9                	INIADR		EQU		00E9H	;表示している画面のオフセット値 R
                      				;EQU	00EAH
  00EB                	INIADW		EQU		00EBH	;アクセスしている画面のオフセット値 R
                      				;EQU	00ECH
  00F6                	PRRIOF		EQU		00F6H	;青のパレット R
  00F7                	GPRIOF		EQU		00F7H	;赤のパレット R
  00F8                	BPRIOF		EQU		00F8H	;緑のパレット R
  00F9                	TPRIOF		EQU		00F9H	;テキストの優先順位 R
  0366                	REPTF1		EQU		0366H	;リピートON/OFF R/W
  0A8B                	SCRMOD		EQU		0A8BH	;グラフィックのクリア指定 R/W
  0E90                	CLICKF		EQU		0E90H	;クリック音の制御フラグ R/W
  0EA5                	KBUFSW		EQU		0EA5H	;1行入力時、先行入力を捨てるかどうかのフラグ R/W
  0EA6                	POINT1		EQU		0EA6H	;INBUF内の書き込みポインタ R/W
  0EA7                	POINT2		EQU		0EA7H	;INBUF内読み込みポインタ R/W
  0EA8                	INBUF		EQU		0EA8H	;～0EE7H 先行入力およびファンクション・キー入力のためのデータ・バッファ R/W
  0EF2                	TABBUF		EQU		0EF2H	;～0F41H 水平タブ設定バッファ
  0F42                	FUNBUF		EQU		0F42H	;～0FE1H ファンクション・キーが定義されているワーク R/W
  1472                	FILOUT		EQU		1472H	;プリンタ・モード R/W
  1480                	DIRIMG		EQU		1480H	;～149FH FCB R/W
  FF00                	IPLFCB		EQU		0FF00H
  FF01                	FNAME		EQU		IPLFCB+1
  FF12                	FSIZE		EQU		IPLFCB+18
  FF14                	SADRS		EQU		IPLFCB+20
  FF16                	EXEAD		EQU		IPLFCB+22
  145A                	WRTMES		EQU		145AH	;CMT WRITNG MESSAGE R
  1462                	FINMES		EQU		1462H	;CMT FIND MESSAGE R
  146A                	SKPMES		EQU		146AH	;CMT SKIP MESSAGE R
                      	
                      	;07CH PORTA 送信データ(下位4ビット)
                      	;07DH PORTB 受信データ(8ビット)
                      	;
                      	;07EH PORTC Bit
                      	;7 IN  CHK
                      	;6 IN
                      	;5 IN
                      	;4 IN 
                      	;3 OUT
                      	;2 OUT FLG
                      	;1 OUT
                      	;0 OUT
                      	;
                      	;07FH コントロールレジスタ
                      	
  14A0                				ORG		14A0H
  14A0  C3A914        				JP		START
  14A3  C35515        	F_COM:		JP		F_COMMAND
  14A6  C35517        	L_COM:		JP		L_COMMAND
                      	
  14A9                	START:
  14A9  CDD614        				CALL	INIT
  14AC  11B514        				LD		DE,TITLE
  14AF  CD0B00        				CALL	PRINT
  14B2  C3E20F        				JP		MONOP
                      	
  14B5  20202020202020	TITLE:		DB		'          ** X1_SD Launcher **',0AH,0DH,00H
                      	
                      	;**** 8255初期化 ****
                      	;PORTC下位BITをOUTPUT、上位BITをINPUT、PORTBをINPUT、PORTAをOUTPUT
  14D6  3E8A          	INIT:		LD		A,8AH
  14D8  017F00        				LD		BC,007FH
  14DB  ED79          				OUT		(C),A
                      	;出力BITをリセット
  14DD  3E00          	INIT2:		LD		A,00H      ;PORTA <- 0
  14DF  017C00        				LD		BC,007CH
  14E2  ED79          				OUT		(C),A
  14E4  017E00        				LD		BC,007EH
  14E7  ED79          				OUT		(C),A   ;PORTC <- 0
                      	
                      	;**** Fコマンド、Lコマンドジャンプ先設定
  14E9  21A314        				LD		HL,F_COM
  14EC  225010        				LD		(1050H),HL
  14EF  21A614        				LD		HL,L_COM
  14F2  225910        				LD		(1059H),HL
  14F5  C9            				RET
                      	
                      	;**** 1BYTE受信 ****
                      	;受信DATAをAレジスタにセットしてリターン
  14F6                	RCVBYTE:
  14F6  C5            				PUSH	BC
  14F7  CD1415        				CALL	F1CHK      ;PORTC BIT7が1になるまでLOOP
  14FA  017D00        				LD		BC,007DH
  14FD  ED78          				IN		A,(C)   ;PORTB -> A
  14FF  F5            				PUSH 	AF
  1500  3E05          				LD		A,05H
  1502  017F00        				LD		BC,007FH
  1505  ED79          				OUT		(C),A    ;PORTC BIT2 <- 1
  1507  CD1E15        				CALL	F2CHK      ;PORTC BIT7が0になるまでLOOP
  150A  3E04          				LD		A,04H
  150C  017F00        				LD		BC,007FH
  150F  ED79          				OUT		(C),A    ;PORTC BIT2 <- 0
  1511  F1            				POP 	AF
  1512  C1            				POP		BC
  1513  C9            				RET
                      	
                      	;**** BUSYをCHECK(1) ****
                      	; 7EH BIT7が1になるまでLOP
  1514  017E00        	F1CHK:		LD		BC,007EH
  1517  ED78          				IN		A,(C)
  1519  E680          				AND		80H        ;PORTC BIT7 = 1?
  151B  28F7          				JR		Z,F1CHK
  151D  C9            				RET
                      	
                      	;**** BUSYをCHECK(0) ****
                      	; 7EH BIT7が0になるまでLOOP
  151E  017E00        	F2CHK:		LD		BC,007EH
  1521  ED78          				IN		A,(C)
  1523  E680          				AND		80H        ;PORTC BIT7 = 0?
  1525  20F7          				JR		NZ,F2CHK
  1527  C9            				RET
                      	
                      	;**** 1BYTE送信 ****
                      	;Aレジスタの内容をPORTA下位4BITに4BITずつ送信
  1528                	SNDBYTE:
  1528  C5            				PUSH	BC
  1529  F5            				PUSH	AF
  152A  1F            				RRA
  152B  1F            				RRA
  152C  1F            				RRA
  152D  1F            				RRA
  152E  E60F          				AND		0FH
  1530  CD3B15        				CALL	SND4BIT
  1533  F1            				POP		AF
  1534  E60F          				AND		0FH
  1536  CD3B15        				CALL	SND4BIT
  1539  C1            				POP		BC
  153A  C9            				RET
                      	
                      	;**** 4BIT送信 ****
                      	;Aレジスタ下位4ビットを送信する
  153B                	SND4BIT:
  153B  017C00        				LD		BC,007CH
  153E  ED79          				OUT		(C),A
  1540  3E05          				LD		A,05H
  1542  017F00        				LD		BC,007FH
  1545  ED79          				OUT		(C),A    ;PORTC BIT2 <- 1
  1547  CD1415        				CALL	F1CHK      ;PORTC BIT7が1になるまでLOOP
  154A  3E04          				LD		A,04H
  154C  017F00        				LD		BC,007FH
  154F  ED79          				OUT		(C),A    ;PORTC BIT2 <- 0
  1551  CD1E15        				CALL	F2CHK
  1554  C9            				RET
                      			
  1555                	F_COMMAND:
  1555                	FC0:		
  1555  1A            				LD		A,(DE)			;スペース読み飛ばし
  1556  FE00          				CP		00H
  1558  2809          				JR		Z,FC2
  155A  13            				INC		DE
  155B  FE20          				CP		20H
  155D  2002          				JR		NZ,FC1
  155F  18F4          				JR		FC0
  1561  1B            	FC1:		DEC		DE
  1562  1B            				DEC		DE
  1563                	FC2:
  1563  217115        				LD		HL,DEFDIR         ;行頭に'*L 'を付けることでカーソルを移動させリターンで実行できるように
  1566  010300        				LD		BC,DEND-DEFDIR
  1569  CD7415        				CALL	DIRLIST
  156C  A7            				AND		A                 ;00以外ならERROR
  156D  C22417        				JP		NZ,SVERR
  1570  C9            				RET
                      	
  1571                	DEFDIR:
  1571  2A4C20        			DB		'*L '
  1574                	DEND:
                      	
                      	;**** DIRLIST本体 (HL=行頭に付加する文字列の先頭アドレス BC=行頭に付加する文字列の長さ) ****
                      	;****              戻り値 A=エラーコード ****
  1574                	DIRLIST:
  1574  3E83          				LD		A,83H      ;DIRLISTコマンド83Hを送信
  1576  CDE016        				CALL	STCD       ;コマンドコード送信
  1579  A7            				AND		A          ;00以外ならERROR
  157A  C20316        				JP		NZ,DLRET
                      			
  157D  C5            				PUSH	BC
  157E  0621          				LD		B,21H
  1580  1A            	STLT1:		LD		A,(DE)
  1581  CD5114        				CALL	TUPPER
  1584  CD2815        	STLT2:		CALL	SNDBYTE           ;比較文字列を送信
  1587  13            				INC		DE
  1588  10F6          				DJNZ	STLT1
  158A  C1            				POP		BC
  158B                	DL1:
  158B  E5            				PUSH	HL
  158C  C5            				PUSH	BC
  158D  110416        				LD		DE,BUF
  1590  EDB0          				LDIR
  1592  EB            				EX		DE,HL
  1593  CDF614        	DL2:		CALL	RCVBYTE           ;'00H'を受信するまでを一行とする
  1596  FE00          				CP		00H
  1598  280C          				JR		Z,DL3
  159A  FEFF          				CP		0FFH              ;'0FFH'を受信したら終了
  159C  2816          				JR		Z,DL4
  159E  FEFE          				CP		0FEH              ;'0FEH'を受信したら一時停止して一文字入力待ち
  15A0  2819          				JR		Z,DL5
  15A2  77            				LD		(HL),A
  15A3  23            				INC		HL
  15A4  18ED          				JR		DL2
  15A6  77            	DL3:		LD		(HL),A
  15A7  110416        				LD		DE,BUF             ;'00H'を受信したら一行分を表示して改行
  15AA  CD0B00        				CALL	PRINT
  15AD  CDA304        				CALL	CR2
  15B0  C1            				POP		BC
  15B1  E1            				POP		HL
  15B2  18D7          				JR		DL1
  15B4  CDF614        	DL4:		CALL	RCVBYTE           ;状態取得(00H=OK)
  15B7  C1            				POP		BC
  15B8  E1            				POP		HL
  15B9  1848          				JR		DLRET
                      	
  15BB  115416        	DL5:		LD		DE,MSG_KEY1        ;HIT ANT KEY表示
  15BE  CD0B00        				CALL	PRINT
  15C1  3E1E          				LD		A,01EH
  15C3  CDC804        				CALL	ACCDIS
  15C6  116916        				LD		DE,MSG_KEY2        ;HIT ANT KEY表示
  15C9  CD0B00        				CALL	PRINT
  15CC  CDA304        				CALL	CR2
  15CF  3E01          	DL6:		LD		A,01H
  15D1  CD1B00        				CALL	INKEY$            ;1文字入力待ち
  15D4  CD5114        				CALL	TUPPER
  15D7  FE00          				CP		00H
  15D9  28F4          				JR		Z,DL6
  15DB  FE03          				CP		03H               ;SHIFT+BREAKで打ち切り
  15DD  281A          				JR		Z,DL7
  15DF  FE1B          				CP		1BH               ;ESCで打ち切り
  15E1  2816          				JR		Z,DL7
  15E3  FE1E          				CP		1EH               ;カーソル↑で打ち切り
  15E5  2808          				JR		Z,DL9
  15E7  FE42          				CP		42H               ;「B」で前ページ
  15E9  2810          				JR		Z,DL8
  15EB  3E00          				LD		A,00H             ;それ以外で継続
  15ED  180C          				JR		DL8
  15EF  3E1E          	DL9:		LD		A,01EH            ;カーソル↑で打ち切ったときにカーソル2行上へ
  15F1  CD1300        				CALL	ACCPRT
  15F4  3E1E          				LD		A,01EH
  15F6  CD1300        				CALL	ACCPRT
  15F9  3EFF          	DL7:		LD		A,0FFH            ;0FFH中断コードを送信
  15FB  CD2815        	DL8:		CALL	SNDBYTE
  15FE  CDA304        				CALL	CR2
  1601  1890          				JR		DL2
                      			
  1603                	DLRET:		
  1603  C9            				RET
                      			
  1604                	BUF:		DS		80
                      	
  1654                	MSG_KEY1:
  1654  4E4558543A414E				DB		'NEXT:ANY BACK:B BRK:'
  1668  00            				DB		00H
  1669                	MSG_KEY2:
  1669  206F7220455343				DB		' or ESC or SHT+BRK'
  167B  00            				DB		00H
                      			
  167C                	MSG_LD:
  167C  4C4F4144494E47			DB		'LOADING '
  1684  00            			DB		00H
                      	
  1685                	MSG_FNAME:
  1685  46494C45204E41			DB		'FILE NAME FAILED!'
  1696  00            			DB		00H
                      			
  1697                	MSG_CMD:
  1697  434F4D4D414E44			DB		'COMMAND FAILED!'
  16A6  00            			DB		00H
                      			
  16A7                	MSG_F0:
  16A7  53442D43415244			DB		'SD-CARD INITIALIZE ERROR'
  16BF  00            			DB		00H
                      			
  16C0                	MSG_F1:
  16C0  4E4F542046494E			DB		'NOT FIND FILE'
  16CD  00            			DB		00H
                      			
  16CE                	MSG_F3:
  16CE  46494C45204558			DB		'FILE EXIST'
  16D8  00            			DB		00H
                      			
  16D9                	MSG99:
  16D9  204552524F52  			DB		' ERROR'
  16DF  00            			DB		00H
                      			
                      	;**** コマンド送信 (IN:A コマンドコード)****
  16E0  CD2815        	STCD:		CALL	SNDBYTE    ;Aレジスタのコマンドコードを送信
  16E3  CDF614        				CALL	RCVBYTE    ;状態取得(00H=OK)
  16E6  C9            				RET
                      	
                      	;**** コマンド、ファイル名送信 (IN:A コマンドコード HL:ファイルネームの先頭)****
  16E7  1B            	STCMD:		DEC		DE
  16E8  CDFC16        				CALL	STFN       ;ファイルネーム取得
  16EB  E5            				PUSH	HL
  16EC  CDE016        				CALL	STCD       ;コマンドコード送信
  16EF  E1            				POP		HL
  16F0  A7            				AND		A          ;00以外ならERROR
  16F1  C22317        				JP		NZ,SVER0
  16F4  CD0B17        				CALL	STFS       ;ファイルネーム送信
  16F7  A7            				AND		A          ;00以外ならERROR
  16F8  C22317        				JP		NZ,SVER0
  16FB  C9            				RET
                      	
                      	;****** FILE NAME 取得 (IN:DE コマンド文字の次の文字 OUT:HL ファイルネームの先頭)*********
  16FC  F5            	STFN:		PUSH	AF
  16FD  13            	STFN1:		INC		DE         ;ファイルネームまでスペース読み飛ばし
  16FE  1A            				LD		A,(DE)
  16FF  FE20          				CP		20H
  1701  28FA          				JR		Z,STFN1
  1703  FE30          				CP		30H        ;「0」以上の文字でなければエラーとする
  1705  DA1E17        				JP		C,STSV2
  1708  EB            				EX		DE,HL
  1709  F1            				POP		AF
  170A  C9            				RET
                      	
                      	;**** ファイルネーム送信(IN:HL ファイルネームの先頭) ******
  170B  0620          	STFS:		LD		B,20H
  170D  7E            	STFS1:		LD		A,(HL)     ;FNAME送信
  170E  CD2815        				CALL	SNDBYTE
  1711  23            				INC		HL
  1712  05            				DEC		B
  1713  20F8          				JR		NZ,STFS1
  1715  3E0D          				LD		A,0DH
  1717  CD2815        				CALL	SNDBYTE
  171A  CDF614        				CALL	RCVBYTE    ;状態取得(00H=OK)
  171D  C9            				RET
                      	
  171E                	STSV2:                      ;ファイルネームの取得に失敗
  171E  118516        				LD		DE,MSG_FNAME
  1721  182B          				JR		ERRMSG
  1723                	SVER0:
  1723  D1            				POP		DE         ;CALL元STACKを破棄する
  1724                	SVERR:
  1724  FEF0          				CP		0F0H
  1726  2005          				JR		NZ,ERR3
  1728  11A716        				LD		DE,MSG_F0  ;SD-CARD INITIALIZE ERROR
  172B  1821          				JR		ERRMSG
  172D  FEF1          	ERR3:		CP		0F1H
  172F  2005          				JR		NZ,ERR4
  1731  11C016        				LD		DE,MSG_F1  ;NOT FIND FILE
  1734  1818          				JR		ERRMSG
  1736  FEF3          	ERR4:		CP		0F3H
  1738  2005          				JR		NZ,ERR5
  173A  11CE16        				LD		DE,MSG_F3  ;FILE EXIST
  173D  180F          				JR		ERRMSG
  173F  FEF4          	ERR5:		CP		0F4H
  1741  2005          				JR		NZ,ERR99
  1743  119716        				LD		DE,MSG_CMD
  1746  1806          				JR		ERRMSG
  1748  CD0712        	ERR99:		CALL	ACHXPR
  174B  11D916        				LD		DE,MSG99   ;その他ERROR
  174E  CD0B00        	ERRMSG:		CALL	PRINT
  1751  CDA304        				CALL	CR2
  1754  C9            	MON:		RET
                      	
  1755                	L_COMMAND:
                      	;**** LOAD ****
                      	;受信ヘッダ情報をセットし、SDカードからLOAD実行
  1755  F3            	SDLOAD:		DI
  1756  3E81          				LD		A,81H  ;LOADコマンド81H
  1758  CDE716        				CALL	STCMD
  175B  CD7817        				CALL	HDRCV      ;ヘッダ情報受信
                      	
  175E  3EE4          				LD		A,0E4H     ;サブCPU割込み停止
  1760  CDFE0D        				CALL	COMOUT
  1763  AF            				XOR		A
  1764  CD540B        				CALL	TRANS49
                      	
  1767  310000        				LD		SP,0000H
                      	
  176A  219917        				LD		HL,DBRCV0       ;実データ読み込み処理をFF20Hへ転送
  176D  1120FF        				LD		DE,DBRCV
  1770  015E00        				LD		BC,ENT6-DBRCV
  1773  EDB0          				LDIR
                      				
  1775  C320FF        	SDLOAD2:	JP		DBRCV      ;データ受信
                      	
                      	;ヘッダ受信
  1778  2100FF        	HDRCV:		LD		HL,IPLFCB
  177B  0620          				LD		B,20H
  177D  CDF614        	HDRC1:		CALL	RCVBYTE    ;IPL用FCB受信
  1780  77            				LD		(HL),A
  1781  23            				INC		HL
  1782  10F9          				DJNZ	HDRC1
  1784  2111FF        				LD		HL,FNAME+16
  1787  AF            				XOR		A
  1788  77            				LD		(HL),A
  1789  117C16        				LD		DE,MSG_LD  ;ファイルネームLOADING表示
  178C  CD0B00        				CALL	PRINT
  178F  1101FF        				LD		DE,FNAME
  1792  CD0B00        				CALL	PRINT
  1795  CDA304        				CALL	CR2
  1798  C9            				RET
                      	
                      	;データ受信
  1799                	DBRCV0:
  FF20                				ORG		0FF20H
                      				
  FF20  F3            	DBRCV:		DI
  FF21  2A16FF        				LD		HL,(EXEAD)
  FF24  E5            				PUSH	HL
  FF25  2A14FF        				LD		HL,(SADRS)
  FF28  ED5B12FF      				LD		DE,(FSIZE)
  FF2C  CD4CFF        	DBRLOP:		CALL	RCVBYTE2
  FF2F  77            				LD		(HL),A
  FF30  1B            				DEC		DE
  FF31  7A            				LD		A,D
  FF32  B3            				OR		E
  FF33  23            				INC		HL
  FF34  20F6          				JR		NZ,DBRLOP   ;DE=0までLOOP
                      	
  FF36  ED4B14FF      				LD		BC,(SADRS)
  FF3A  78            				LD		A,B
  FF3B  B1            				OR		C
  FF3C  280C          				JR		Z,DBRLOP1
  FF3E  0B            				DEC		BC
  FF3F  0B            				DEC		BC
  FF40  210000        				LD		HL,0000H    ;読み込み開始位置が0000Hでなかったら念のため、0000H～読み込み開始位置までのメモリをクリア
  FF43  110100        				LD		DE,0001H
  FF46  3600          				LD		(HL),00H
  FF48  EDB0          				LDIR
                      	
  FF4A                	DBRLOP1:
  FF4A  E1            				POP		HL
  FF4B  E9            				JP		(HL)        ;実行番地へジャンプ
                      	
                      	;**** 1BYTE受信 ****
                      	;受信DATAをAレジスタにセットしてリターン
  FF4C                	RCVBYTE2:
  FF4C  C5            				PUSH	BC
  FF4D  CD6AFF        				CALL	F1CHK2      ;PORTC BIT7が1になるまでLOOP
  FF50  017D00        				LD		BC,007DH
  FF53  ED78          				IN		A,(C)   ;PORTB -> A
  FF55  F5            				PUSH 	AF
  FF56  3E05          				LD		A,05H
  FF58  017F00        				LD		BC,007FH
  FF5B  ED79          				OUT		(C),A    ;PORTC BIT2 <- 1
  FF5D  CD74FF        				CALL	F2CHK2      ;PORTC BIT7が0になるまでLOOP
  FF60  3E04          				LD		A,04H
  FF62  017F00        				LD		BC,007FH
  FF65  ED79          				OUT		(C),A    ;PORTC BIT2 <- 0
  FF67  F1            				POP 	AF
  FF68  C1            				POP		BC
  FF69  C9            				RET
                      	
                      	;**** BUSYをCHECK(1) ****
                      	; 7EH BIT7が1になるまでLOP
  FF6A  017E00        	F1CHK2:		LD		BC,007EH
  FF6D  ED78          				IN		A,(C)
  FF6F  E680          				AND		80H        ;PORTC BIT7 = 1?
  FF71  28F7          				JR		Z,F1CHK2
  FF73  C9            				RET
                      	
                      	;**** BUSYをCHECK(0) ****
                      	; 7EH BIT7が0になるまでLOOP
  FF74  017E00        	F2CHK2:		LD		BC,007EH
  FF77  ED78          				IN		A,(C)
  FF79  E680          				AND		80H        ;PORTC BIT7 = 0?
  FF7B  20F7          				JR		NZ,F2CHK2
  FF7D  C9            				RET
                      	
  FF7E                	ENT6:
  FF7E                				END
